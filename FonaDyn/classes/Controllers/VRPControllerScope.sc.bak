// Copyright (C) 2016-2023 by Sten Ternstr√∂m & Dennis J. Johansson, KTH Stockholm
// Released under European Union Public License v1.2, at https://eupl.eu
// *** EUPL *** //
VRPControllerScope {
	var mData;
	var mDataMaps;
	var mTarget;
	var mSynthPrepare; // Prepare step for the BusListeners - generating timestamps/audio rate gate etc

	var mBufferMovingEGG;
	var mSynthMovingEGG;
	var mConditionMovingEGG;
	var mBufferRequesterMovingEGG;
	var mRequestMovingEGG;

	var mBusListenerGoodCycles; // Per cycle bus listener using only good cycles
	var mBusListenerCycleSeparation; // Per cycle bus listener using the raw cycles (unfiltered gate from the cycle separation)

	// States
	var mRunning;

	*new { | target, data |
		^super.new.init(target, data);
	}

	// Init given the target and output data structure
	// This function is called once only!
	init { | target, data |
		mTarget = target;
		mData = data;
		mBusListenerGoodCycles = BusListener();
		mBusListenerCycleSeparation = BusListener();
	}

	// Tell the busManager what buses it requires.
	// This function is called before prepare.
	requires { | busManager |
		var d = mData;
		var s = d.settings;

		busManager
		.requireAudio(\Timestamp)
		.requireAudio(\SampEn)
		.requireAudio(\GateCycle)
		.requireAudio(\GateFilteredDFT)
		.requireAudio(\DEGGmax)
		.requireAudio(\Qcontact)
		.requireAudio(\Icontact)
		.requireAudio(\HRFEGG)
		.requireAudio(\ClusterNumber)
		.requireControl(\PhonClusterNumber)
		.requireControl(\Frequency)
		.requireControl(\Amplitude)
		.requireControl(\Clarity)
		.requireControl(\Crest)
		.requireControl(\SpecBal)
		.requireControl(\CPPsmoothed)
		.requireControl(\NoiseThreshold)
		.requireAudio(\DelayedFrequency)
		.requireAudio(\DelayedAmplitude)
		.requireAudio(\DelayedClarity)
		.requireAudio(\DelayedSpecBal);
	}

	// Prepare to start - init the SynthDefs and allocate Buffers.
	// This function is always called before start.
	prepare { | libname, server, busManager, clock |
		var bm = busManager;
		var d = mData;
		var sd = d.scope;
		var s = d.settings;
		var ss = s.scope;
		var vrpd = d.vrp;

		// Generate a base path for the output
		var dir = s.general.output_directory;
		var timestamp = d.general.timestamp;
		var base_path = dir +/+ timestamp;

		// Create 2 bus assignment tools for both rates
		var cycle_tool = BusListener.newBusAssignmentTool;
		var cycle_separation_tool = BusListener.newBusAssignmentTool;
		var graphsToDraw;

		sd.busThreshold = bm.control(\NoiseThreshold);

		// Compile the synthdefs
		VRPSDScope.compile(libname, ss.movingEGGCount, ss.minSamples, ss.maxSamples, ss.normalize);

		// Create the directory if necessary.
		if (File.type(dir) != \directory, { File.mkdir(dir); });

		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		// Add the handlers for the cycle rate bus listener
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////

		graphsToDraw = [\Timestamp] ++ s.sampen.graphsRequested;
		cycle_tool.assign(\SampEnScope,
			graphsToDraw collect: { | b, i |
				// if (b == \Crest, { bm.control(b) }, { bm.audio(b) } )
				// if (b == \SpecBal, { bm.control(b) }, { bm.audio(b) } )
				if (b == \CPPsmoothed, { bm.control(b) }, { bm.audio(b) } )
			}
		);

		cycle_tool.assign(\VRPData,
			[
				bm.audio(\DelayedFrequency),
				bm.audio(\DelayedAmplitude),
				bm.audio(\DelayedClarity),
				bm.audio(\DelayedCrest),
				bm.audio(\DelayedSpecBal),
				bm.control(\CPPsmoothed),   // might not need Delayed since it's a long PV_Chain
				bm.audio(\SampEn),
				bm.audio(\DEGGmax),
				bm.audio(\Qcontact),
				bm.audio(\Icontact),
				bm.audio(\HRFEGG),
				bm.audio(\ClusterNumber),
				bm.control(\PhonClusterNumber)
			]
		);

		// Prepare an ordered array of VRPDataLayer.mapData objects
		// - they are DrawableSparseMatrix'es. It must match the sequence
		// returned by the \VRPData  handler into the 'frame' argument.
		mDataMaps =
		[\Density, \Clarity, \Crest, \SpecBal, \CPP, \CSE, \Qdelta, \Qci, \Ic, \HRFegg, \ClustersEGG, \ClustersPhon]
		collect: { | sym, ix | vrpd.layers[sym].mapData };

		// Add the handler to grab scope data for the Plots scope
		mBusListenerGoodCycles
		.addHandler(\scopeSampEn, \scope,
			cycle_tool.indices(\SampEnScope),
			ss.duration,
			{
				| data |
				sd.sampen = data;
			}
		)

		// Add the handler to grab the VRPData
		.addHandler(\vrp, \custom, {
			| data |
			data.dataAsFrames do: { | frame |
				var cycleData;
				var freq;
				var amp;
				var clarity;
				var crest;
				var specBal;
				var cppSmoothed;
				var entropy;
				var dEGGmax;
				var qContact;
				var iContact;
				var hrfEGG;
				var clusterNumber;
				var phonClusterNumber;

				var idx_midi;
				var idx_spl;

				#freq, amp, clarity, crest, specBal, cppSmoothed,
				entropy, dEGGmax, qContact, iContact, hrfEGG, clusterNumber, phonClusterNumber
				= frame[ cycle_tool.indices(\VRPData) ];

				// Set up variables to receive the data
				// #freq, amp ... cycleData = frame[ cycle_tool.indices(\VRPData) ];

				// There is a grid that is VRPDataVRP.vrpWidth x VRPDataVRP.vrpHeight in size,
				// that we need to map these values to
				idx_midi = VRPDataVRP.frequencyToIndex( freq );
				idx_spl = VRPDataVRP.amplitudeToIndex( amp );

				// A clarity TEST IS NOT NEEDED
				// SINCE THIS HANDLER IS TRIGGERED BY \GateFilteredDFT
				value {
					var invDensity, putMeanArgs;
					var best_count = 0;
					var best_cluster_idx = 0;
					var percent = 0;
					var densityNew = vrpd.density.increment(idx_spl, idx_midi, 1);
					invDensity = densityNew.reciprocal;

					putMeanArgs = [idx_spl, idx_midi, densityNew-1, invDensity];
					vrpd.crest.putMean(putMeanArgs, crest);
					vrpd.specBal.putMean(putMeanArgs, specBal);
					vrpd.cppSmoothed.putMean(putMeanArgs, cppSmoothed);
					vrpd.entropy.putMean(putMeanArgs, entropy);
					vrpd.dEGGmax.putMean(putMeanArgs, dEGGmax);
					vrpd.qContact.putMean(putMeanArgs, qContact);
					vrpd.iContact.putMean(putMeanArgs, iContact);
					vrpd.hrfEGG.putMean(putMeanArgs, hrfEGG);

					// Update maxDensity
					if ( (vrpd.maxDensity ? 0) < densityNew, {
						vrpd.maxDensity = densityNew;
					});

					vrpd.clustersEGG.increment(idx_spl, idx_midi, clusterNumber+1);
					vrpd.clustersEGG.setMaxCluster(idx_spl, idx_midi, densityNew);

					vrpd.clustersPhon.increment(idx_spl, idx_midi, phonClusterNumber+1);
					vrpd.clustersPhon.setMaxCluster(idx_spl, idx_midi, densityNew);

				};  // End value

				vrpd.currentCluster = clusterNumber;
				vrpd.currentClusterPhon = phonClusterNumber;
			}; // End do

		});

		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////

		cycle_separation_tool
		.assign(\vrp,
			[
				bm.control(\Frequency),
				bm.control(\Amplitude),
				bm.control(\Clarity)
			]
		);

		mBusListenerCycleSeparation
		.addHandler(\vrp, \custom, {
			| data |
			var freq, amp, clarity;
			var indices = cycle_separation_tool.indices(\vrp);

			data.dataAsFrames do: { | frame |
				var idx_midi;
				var idx_spl;

				#freq, amp, clarity = frame[ indices ];

				// There is a grid that is VRPDataVRP.vrpWidth x VRPDataVRP.vrpHeight in size,
				// to which we need to map these values
				idx_midi = VRPDataVRP.frequencyToIndex( freq );
				idx_spl = VRPDataVRP.amplitudeToIndex( amp );

				// Set the clarity measurement
				vrpd.clarity.put(idx_spl, idx_midi,
					max( clarity, vrpd.clarity.at(idx_spl, idx_midi) ? 0 )
				);

			};

			// Update the current Frequency and Amplitude
			vrpd.currentFrequency = freq;
			vrpd.currentAmplitude = amp;
			vrpd.currentClarity = clarity;
		});

		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		// Prepare the bus listeners
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		mBusListenerGoodCycles.prepare(libname, server,
			cycle_tool.buses,
			bm.audio(\GateFilteredDFT),
			clock
		);

		mBusListenerCycleSeparation.prepare(libname, server,
			cycle_separation_tool.buses,
			bm.audio(\GateCycle),
			clock
		);

		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		// Prepare for the Moving EGG
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		mBufferMovingEGG = MovingEGG.allocBuffer(server, ss.movingEGGCount, ss.movingEGGSamples);
		mBufferRequesterMovingEGG = BufferRequester(mBufferMovingEGG);
		mConditionMovingEGG = Condition();

		value {
			var bufsize = mBufferMovingEGG.numFrames * mBufferMovingEGG.numChannels;
			var reqs = mBufferRequesterMovingEGG.asRequests(0, bufsize);
			// Assert that we read the entire buffer
			if ( reqs.size != 1, { Error("Assertion failed!").postln; });
			mRequestMovingEGG = reqs[0];
		};
	}

	// Start - Create the synths, and initiate fetching of data at the regular
	// interval given by the clock parameter.
	// This function is always called between prepare and stop.
	start { | server, busManager, clock |
		var bm = busManager;

		// Start the preparation synth
		mSynthPrepare = Synth(*VRPSDScope.prepare(
			bm.audio(\Timestamp),
			mTarget,
			\addToTail)
		);

		// Start the bus listener
		mBusListenerGoodCycles.start(server, mTarget, clock);
		mBusListenerCycleSeparation.start(server, mTarget, clock);

		// Init movingEGG
		mSynthMovingEGG = Synth(*VRPSDScope.movingEGG(
			bm.audio(\GateCycle),
			bm.audio(\ConditionedEGG),
			mBufferMovingEGG,
			mTarget,
			\addToTail)
		);

		// Start fetching the buffer for the movingEGG.
		mRunning = true;
		clock.sched(1, {
			if ( mRunning, {
				// Request the contents of the buffer - no need to resend lost,
				// since we're only interested in the most fresh data
				mBufferRequesterMovingEGG.sendAll(mRequestMovingEGG.asArray);
			}, {
				// Resend the final request if it got lost
				if ( mConditionMovingEGG.test.not and: (mRequestMovingEGG.duration > mBufferRequesterMovingEGG.lostDuration), {
					mBufferRequesterMovingEGG.sendAll(mRequestMovingEGG.asArray);
				});
			});

			if ( mRequestMovingEGG.data.notNil and: mConditionMovingEGG.test.not, {
				// Dispatch request data
				mData.scope.movingEGGData = mRequestMovingEGG.data;
				mRequestMovingEGG.data = nil;

				if ( mRunning.not, {
					// Signal that the final request has been processed!
					mConditionMovingEGG.test = true; // Don't block on waits
					mConditionMovingEGG.signal; // Signal that stop can continue and free the buffer
				});
			});

			// Continue if:
			if (mRunning or: mConditionMovingEGG.test.not, 3, nil)
		});
	}

	// Free the synths and buffers after finishing fetching data.
	// The synths are guaranteed to be paused at this point - so buffers should be stable.
	stop {
		// Free the synths
		mSynthPrepare.free;
		mSynthMovingEGG.free;

		// Stop the bus listeners
		mBusListenerGoodCycles.stop;
		mBusListenerCycleSeparation.stop;

		// Signal that we want to wait on the condition
		mConditionMovingEGG.test = false;

		// Copy the request to ensure that we will make one final read after stop
		mRequestMovingEGG = mRequestMovingEGG.deepCopy;
		mRequestMovingEGG.data = nil;

		mBufferRequesterMovingEGG.sendAll(mRequestMovingEGG.asArray);

		// Tell the scheduled reads to stop
		mRunning = false;
	}

	sync {
		mBusListenerGoodCycles.sync;
		mBusListenerCycleSeparation.sync;

		mConditionMovingEGG.wait;
		// Ok to free the buffers now
		mBufferMovingEGG.free;
	}
}